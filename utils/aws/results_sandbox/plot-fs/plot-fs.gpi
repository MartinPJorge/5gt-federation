set term eps
set output "/tmp/fs.eps"

# Color palette taken from
# http://www.gnuplotting.org/ease-your-plotting-with-config-snippets/
# line styles
set style line 1 lt 1 lc rgb '#1B9E77' # dark teal
set style line 2 lt 1 lc rgb '#D95F02' # dark orange
set style line 3 lt 1 lc rgb '#7570B3' # dark lilac
set style line 4 lt 1 lc rgb '#E7298A' # dark magenta
set style line 5 lt 1 lc rgb '#66A61E' # dark lime green
set style line 6 lt 1 lc rgb '#E6AB02' # dark banana
set style line 7 lt 1 lc rgb '#A6761D' # dark tan
set style line 8 lt 1 lc rgb '#666666' # dark gray

set xrange [0:1]
set xlabel "p^{(t)} - normalized AWS spot price"
set ylabel "f(p^{(t)}) - arrival rate [instances/day]"
set xtics 0,0.1,1

set tics scale 0
set grid

f(k, a, b, x) = k * (1 - x**a)**b


# k values
# 5 req/day - large -> 5 / sqrt(1 - 0.5^2)
# 12.5 req/day -mwdium -> 12.5 / sqrt(1 - 0.5^2)
# 25 req/day -small -> 25 / sqrt(1 - 0.5^2)

set label "5" at 0.5,f(5.773502691896258, 2, 0.5, 0.5) point ls 1 pt 28 ps 2.2 front offset   -0.8,1.2
set label "12.5" at 0.5,f(14.433756729740645, 2, 0.5, 0.5) point ls 2 pt 28 ps 2.2 front offset  -2,1.2
set label "25" at 0.5,f(28.86751345948129, 2, 0.5, 0.5) point ls 3 pt 28 ps 2.2 front offset -1.2,1.2

plot f(5.773502691896258, 2, 0.5, x) ls 1 dt 2 lw 12 t "c5d.4xlarge",\
    f(14.433756729740645, 2, 0.5, x) ls 2 dt 4 lw 12 t "c5.2xlarge",\
    f(28.86751345948129, 2, 0.5, x) ls 3 dt 6 lw 12 t "t3a.small",\
    NaN  w points ls 8 ps 1.5 pt 27 t "[LUIS-TID]"


